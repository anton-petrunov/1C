
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Заголовок = "Выберите файл";
	
	ЧтоДелатьПослеВыбораФайла = Новый ОписаниеОповещения("ПослеВыбораФайла", ЭтотОбъект);
	НачатьПомещениеФайлаНаСервер(ЧтоДелатьПослеВыбораФайла,,,,ПараметрыДиалога, УникальныйИдентификатор); 

КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайла(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
	
	Если ОписаниеПомещенногоФайла = Неопределено Тогда
		Закрыть();//закрыть форму
		Возврат;
	КонецЕсли; 
	
	АдресДанных = ОписаниеПомещенногоФайла.Адрес;
	ИнформацияОФайле = ОписаниеПомещенногоФайла.СсылкаНаФайл;
	
	Объект.Наименование = ИнформацияОФайле.Имя;
	Объект.Расширение = ИнформацияОФайле.Расширение;
	
	ЧтоДелатьПослеПолученияРазмера = Новый ОписаниеОповещения("ПослеПолученияРазмера", ЭтотОбъект);
	
	ИнформацияОФайле.Файл.НачатьПолучениеРазмера(ЧтоДелатьПослеПолученияРазмера);

КонецПроцедуры // ПослеВыбораФайла()

&НаКлиенте
Процедура ПослеПолученияРазмера(Размер, ДополнительныеПараметры) Экспорт

	Объект.РазмерФайла = Размер / 1024;

КонецПроцедуры // ПослеПолученияРазмера()

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(АдресДанных) Тогда
		ДанныеФайла = ПолучитьИзВременногоХранилища(АдресДанных);
		ТекущийОбъект.ДанныеФайла = Новый ХранилищеЗначения(ДанныеФайла, Новый СжатиеДанных(9));
	КонецЕсли; 
	
КонецПроцедуры
