
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	АдресЛоготипа = ПолучитьНавигационнуюСсылку(ТекущийОбъект, "Логотип");
	АдресПечати   = ПолучитьНавигационнуюСсылку(ТекущийОбъект, "Печать");
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если ЭтоАдресВременногоХранилища(АдресЛоготипа) Тогда
		ДанныеФайла = ПолучитьИзВременногоХранилища(АдресЛоготипа);
		ТекущийОбъект.Логотип = Новый ХранилищеЗначения(ДанныеФайла, Новый СжатиеДанных(9));
	ИначеЕсли ПустаяСтрока(АдресЛоготипа) = Истина Тогда
		ТекущийОбъект.Логотип = Неопределено;
	КонецЕсли; 

	Если ЭтоАдресВременногоХранилища(АдресПечати) Тогда
		ДанныеФайла = ПолучитьИзВременногоХранилища(АдресПечати);
		ТекущийОбъект.Печать = Новый ХранилищеЗначения(ДанныеФайла, Новый СжатиеДанных(9));
	ИначеЕсли ПустаяСтрока(АдресПечати) = Истина Тогда
		ТекущийОбъект.Печать = Неопределено;
	КонецЕсли; 

	ТекущийОбъект.КонтактнаяИнформация.Очистить();
	Для каждого ОписаниеРеквизита Из ОписаниеКонтактнойИнформации Цикл
		СтрокаТЧ = ТекущийОбъект.КонтактнаяИнформация.Добавить();
		СтрокаТЧ.Тип = ОписаниеРеквизита.Тип;
		СтрокаТЧ.Вид = ОписаниеРеквизита.Вид;
		СтрокаТЧ.Значение = ЭтотОбъект[ОписаниеРеквизита.ИмяРеквизита];
	КонецЦикла; 

КонецПроцедуры          

&НаКлиенте
Процедура ЗагрузитьЛоготип(Команда)
	
	Модифицированность = Истина;

	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Заголовок = "Выберите изображение логотипа";
	ПараметрыДиалога.Фильтр = "Изображение | *.jpg; *.png;*.bmp;*.jpeg";
	
	ЧтоДелатьПослеВыбораФайла = Новый ОписаниеОповещения("ПослеВыбораЛоготипа", ЭтотОбъект);
	НачатьПомещениеФайлаНаСервер(ЧтоДелатьПослеВыбораФайла,,,,ПараметрыДиалога, УникальныйИдентификатор); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораЛоготипа(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт

	Если ОписаниеПомещенногоФайла = Неопределено Тогда
		Возврат;	
	КонецЕсли; 
	
	АдресЛоготипа = ОписаниеПомещенногоФайла.Адрес;   
	
КонецПроцедуры // ПослеВыбораКартинки()

&НаКлиенте
Процедура ЗагрузитьПечать(Команда)
	
	Модифицированность = Истина;
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Заголовок = "Выберите изображение печати";
	ПараметрыДиалога.Фильтр = "Изображение | *.jpg; *.png;*.bmp;*.jpeg";
	
	ЧтоДелатьПослеВыбораФайла = Новый ОписаниеОповещения("ПослеВыбораПечати", ЭтотОбъект);
	НачатьПомещениеФайлаНаСервер(ЧтоДелатьПослеВыбораФайла,,,,ПараметрыДиалога, УникальныйИдентификатор); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПечати(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт

	Если ОписаниеПомещенногоФайла = Неопределено Тогда
		Возврат;	
	КонецЕсли; 
	
	АдресПечати = ОписаниеПомещенногоФайла.Адрес;

КонецПроцедуры // ПослеВыбораКартинки()

&НаКлиенте
Процедура ОчиститьЛоготип(Команда)
	
	АдресЛоготипа = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПечать(Команда)

	АдресПечати = "";

КонецПроцедуры

&НаКлиенте
Процедура АдресЛоготипаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗагрузитьЛоготип(1);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресПечатиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗагрузитьПечать(1);
	
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//1. Заполнение таблицы ОписаниеКонтактнойИнформации
	ГруппаОрганизации = Справочники.ВидыКонтактнойИнформации.Справочник_Организации;
	Выборка = Справочники.ВидыКонтактнойИнформации.Выбрать(ГруппаОрганизации);
	Пока Выборка.Следующий() Цикл
		
		Идентификатор = Новый УникальныйИдентификатор;
		ИмяРеквизита = "Реквизит_" + СтрЗаменить(Идентификатор, "-", "");
		
		ОписаниеРеквизита = ОписаниеКонтактнойИнформации.Добавить();
		ОписаниеРеквизита.ИмяРеквизита = ИмяРеквизита;
		ОписаниеРеквизита.Тип = Выборка.Тип;
		ОписаниеРеквизита.Вид = Выборка.Ссылка;
		ОписаниеРеквизита.Представление = Выборка.Наименование;
		
	КонецЦикла; 
	
	//2. Создание реквизитов формы для хранения контактной информации
	Реквизиты = Новый Массив;
	ТипСтрока = Новый ОписаниеТипов("Строка");
	Для каждого ОписаниеРеквизита Из ОписаниеКонтактнойИнформации Цикл
		Реквизиты.Добавить(Новый РеквизитФормы(ОписаниеРеквизита.ИмяРеквизита, ТипСтрока));
	КонецЦикла; 
	
	ИзменитьРеквизиты(Реквизиты);
	
	//3. Создание элементов формы + заполнение реквизитов формы (которые созданы программно)
	Для каждого ОписаниеРеквизита Из ОписаниеКонтактнойИнформации Цикл
		
		//создание элемента
		ИмяРеквизита = ОписаниеРеквизита.ИмяРеквизита;
		
		НовыйЭлемент = Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Элементы.СтраницаКонтактнаяИнформация);
		НовыйЭлемент.ПутьКДанным = ИмяРеквизита;
		НовыйЭлемент.Заголовок	 = ОписаниеРеквизита.Представление;
		НовыйЭлемент.Вид		 = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.УстановитьДействие("ПриИзменении", "ПриИзмененииКонтактнойИнформации");
		
		//заполнение реквизитов формы
		МассивСтрок = Объект.КонтактнаяИнформация.НайтиСтроки(Новый Структура("Вид", ОписаниеРеквизита.Вид));
		Если МассивСтрок.Количество() > 0 Тогда
			ЭтотОбъект[ИмяРеквизита] = МассивСтрок[0].Значение;
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКонтактнойИнформации()
	Модифицированность = Истина;
КонецПроцедуры // ПриИзмененииКонтактнойИнформации()


