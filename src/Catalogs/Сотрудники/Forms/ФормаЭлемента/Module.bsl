
//1. ВЫБОР КАРТИНКИ И ЕЁ ОТОБРАЖЕНИЕ НА ФОРМЕ
&НаКлиенте
Процедура АдресКартинкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Модифицированность = Истина;
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Заголовок = "Выберите изображение";
	ПараметрыДиалога.Фильтр = "Изображение | *.jpg; *.png;*.bmp;*.jpeg";
	
	ЧтоДелатьПослеВыбораФайла = Новый ОписаниеОповещения("ПослеВыбораФото", ЭтотОбъект);
	НачатьПомещениеФайлаНаСервер(ЧтоДелатьПослеВыбораФайла,,,,ПараметрыДиалога, УникальныйИдентификатор); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФото(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт

	Если ОписаниеПомещенногоФайла = Неопределено Тогда
		Возврат;	
	КонецЕсли; 
	
	АдресФото = ОписаниеПомещенногоФайла.Адрес;

КонецПроцедуры // ()

//2. ЗАПИСЬ КАРТИНКИ В БАЗУ (Заполнение реквизита справочника)
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(АдресФото) Тогда
		ДанныеФото = ПолучитьИзВременногоХранилища(АдресФото);
		ТекущийОбъект.Фотография = Новый ХранилищеЗначения(ДанныеФото, Новый СжатиеДанных(9));
	ИначеЕсли ПустаяСтрока(АдресФото) Тогда
		ТекущийОбъект.Фотография = Неопределено;
	КонецЕсли; 
	
	Если ЭтоАдресВременногоХранилища(АдресПодписи) Тогда
		ДанныеПодписи = ПолучитьИзВременногоХранилища(АдресПодписи);
		ТекущийОбъект.Подпись = Новый ХранилищеЗначения(ДанныеПодписи, Новый СжатиеДанных(9));
	ИначеЕсли ПустаяСтрока(АдресПодписи) Тогда
		ТекущийОбъект.Подпись = Неопределено;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФото(Команда)
	
	АдресФото = "";
	
КонецПроцедуры

//3. ОТОБРАЖЕНИЕ КАРТИНКИ ПРИ ОТРКЫТИИ ФОРМЫ
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	АдресФото = ПолучитьНавигационнуюСсылку(ТекущийОбъект, "Фотография");
	
	АдресПодписи = ПолучитьНавигационнуюСсылку(ТекущийОбъект, "Подпись");
	
КонецПроцедуры

&НаКлиенте
Процедура АдресПодписиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Модифицированность = Истина;
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Заголовок = "Выберите изображение";
	ПараметрыДиалога.Фильтр = "Изображение | *.jpg; *.png;*.bmp;*.jpeg";
	
	ЧтоДелатьПослеВыбораФайла = Новый ОписаниеОповещения("ПослеВыбораПодписи", ЭтотОбъект);
	НачатьПомещениеФайлаНаСервер(ЧтоДелатьПослеВыбораФайла,,,,ПараметрыДиалога, УникальныйИдентификатор); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПодписи(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт

	Если ОписаниеПомещенногоФайла = Неопределено Тогда
		Возврат;	
	КонецЕсли; 
	
	АдресПодписи = ОписаниеПомещенногоФайла.Адрес;

КонецПроцедуры // ()


