
&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	//1. получить данные текущей строки
	ДанныеСтроки = Элементы.Товары.ТекущиеДанные;
	
	//2. Получение ставки НДС и ед. изм. для текущей номенклатуры
	ИменаРеквизитов = "ЕдиницаИзмерения,СтавкаНДС";
	РеквизитыНоменклатуры = ОбщегоНазначенияВызовСервера.ПолучитьРеквизитыНоменклатуры(ДанныеСтроки.Номенклатура, ИменаРеквизитов);
	
	//3. заполнение данных в строке
	Если РеквизитыНоменклатуры = Неопределено Тогда
		ДанныеСтроки.ЕдиницаИзмерения = Неопределено;
		ДанныеСтроки.СтавкаНДС = Неопределено;
	Иначе
		ДанныеСтроки.ЕдиницаИзмерения = РеквизитыНоменклатуры.ЕдиницаИзмерения;
		ДанныеСтроки.СтавкаНДС = РеквизитыНоменклатуры.СтавкаНДС;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	//1. получить данные текущей строки
	ДанныеСтроки = Элементы.Товары.ТекущиеДанные;
	
	//2. рассчитать суммы
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммыВСтрокеТабличнойЧасти(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	//1. получить данные текущей строки
	ДанныеСтроки = Элементы.Товары.ТекущиеДанные;
	
	//2. рассчитать суммы
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммыВСтрокеТабличнойЧасти(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	//1. получить данные текущей строки
	ДанныеСтроки = Элементы.Товары.ТекущиеДанные;
	
	//2. рассчитать суммы
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммыВСтрокеТабличнойЧасти(ДанныеСтроки);
	
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Выборка = Справочники.СтатусыЗаказовПоставщикам.Выбрать(,,,"Порядок");
	Пока Выборка.Следующий() Цикл
		Элементы.Статус.СписокВыбора.Добавить(Выборка.Ссылка);	
	КонецЦикла; 
	
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьПоПоследнемуЗаказу(Команда)
	
	Если Объект.Контрагент.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Выберите поставщика";
		Сообщение.Поле = "Объект.Контрагент";
		Сообщение.Сообщить();		
		Возврат;
	КонецЕсли; 
	
	Если Объект.Товары.Количество() > 0 Тогда
		ЧтоДелатьПослеОтветаНаВопрос = Новый ОписаниеОповещения("ЗаполнитьПоПоследнемуЗаказуЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ЧтоДелатьПослеОтветаНаВопрос, "Табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет);	
	Иначе
		ЗаполнитьПоПоследнемуЗаказуНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПоследнемуЗаказуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
	 	ЗаполнитьПоПоследнемуЗаказуНаСервере();
	КонецЕсли; 
	
КонецПроцедуры // ЗаполнитьПоПоследнемуЗаказуЗавершение()


&НаСервере
Процедура ЗаполнитьПоПоследнемуЗаказуНаСервере()
	
	Объект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗаказПоставщику.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_ДокументыПоставщика
		|ИЗ
		|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
		|ГДЕ
		|	ЗаказПоставщику.Контрагент = &Контрагент
		|	И ЗаказПоставщику.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказПоставщику.МоментВремени УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказПоставщикуТовары.Ссылка КАК Ссылка,
		|	ЗаказПоставщикуТовары.НомерСтроки КАК НомерСтроки,
		|	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
		|	ЗаказПоставщикуТовары.Количество КАК Количество,
		|	ЗаказПоставщикуТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЗаказПоставщикуТовары.Цена КАК Цена,
		|	ЗаказПоставщикуТовары.СтавкаНДС КАК СтавкаНДС,
		|	ЗаказПоставщикуТовары.Сумма КАК Сумма,
		|	ЗаказПоставщикуТовары.СуммаНДС КАК СуммаНДС,
		|	ЗаказПоставщикуТовары.СуммаВсего КАК СуммаВсего
		|ИЗ
		|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДокументыПоставщика КАК ВТ_ДокументыПоставщика
		|		ПО ЗаказПоставщикуТовары.Ссылка = ВТ_ДокументыПоставщика.Ссылка";
	
	Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "По текущему поставщику заказов ещё не было";
		Сообщение.Сообщить();
		
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = Объект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
		КонецЦикла;
	
	КонецЕсли; 
		
КонецПроцедуры


&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ПриИзмененииКонтрагента();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииКонтрагента()

	Объект.Договор = Справочники.Контрагенты.ПолучитьОсновнойДоговорКонтрагента(Объект.Контрагент);

КонецПроцедуры // ПриИзмененииКонтрагента()


