
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Выборка = Справочники.СтатусыЗаказовПоставщикам.Выбрать(,,,"Порядок");
	Пока Выборка.Следующий() Цикл
		
		//1. создание команды
		Идентификатор = Новый УникальныйИдентификатор;
		ИмяКоманды = "Команда_" + СтрЗаменить(Идентификатор, "-", "");
		
		НоваяКоманда = Команды.Добавить(ИмяКоманды);
		НоваяКоманда.Действие = "ИзменитьСтатусЗаказов";
		НоваяКоманда.Заголовок = Выборка.Наименование;
		
		//2. создание элемента формы (кнопка)
		НовыйЭлемент = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Элементы.ПодменюСтатусы);
		НовыйЭлемент.ИмяКоманды = ИмяКоманды;
		НовыйЭлемент.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		
		//3. добавление строки в таблицу статусов
		СтрокаСтатуса = СписокСтатусов.Добавить();
		СтрокаСтатуса.ИмяКоманды = ИмяКоманды;
		СтрокаСтатуса.Статус = Выборка.Ссылка;
		СтрокаСтатуса.Представление = Выборка.Наименование;
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСтатусЗаказов(Команда)

	//1. определение статуса по имени команды
	ИмяКоманды = Команда.Имя;
	
	СтруктураОтбора = Новый Структура("ИмяКоманды", ИмяКоманды);
	НайденныеСтроки = СписокСтатусов.НайтиСтроки(СтруктураОтбора);
	
	НовыйСтатус = НайденныеСтроки[0].Статус;
	
	//2. определить выделенные заказы
	МассивЗаказов = Элементы.Список.ВыделенныеСтроки;
	
	//3. Изменение статусов
	ИзменитьСтатусЗаказовНаСервере(МассивЗаказов, НовыйСтатус);
	
	//4. Обновление динамических спискова
	ОповеститьОбИзменении(Тип("ДокументСсылка.ЗаказПоставщику"));
	
	ПоказатьОповещениеПользователя("Изменение статусов завершено...");
	
КонецПроцедуры // ИзменитьСтатусЗаказов()

&НаСервереБезКонтекста
Процедура ИзменитьСтатусЗаказовНаСервере(МассивЗаказов, НовыйСтатус)

	Для каждого ЗаказПоставщика Из МассивЗаказов Цикл
		
		ДокОбъект = ЗаказПоставщика.ПолучитьОбъект();
		Если ДокОбъект.Статус = НовыйСтатус Тогда
			Продолжить;
		КонецЕсли; 
		
		ДокОбъект.Статус = НовыйСтатус;
		
		РежимЗаписи = ?(ДокОбъект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись);
		ДокОбъект.Записать(РежимЗаписи);
		
	КонецЦикла; 

КонецПроцедуры // ИзменитьСтатусЗаказовНаСервере()

