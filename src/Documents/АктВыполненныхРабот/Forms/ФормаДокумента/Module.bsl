&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ПриИзмененииКонтрагента();
	
КонецПроцедуры   

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	
	ПриИзмененииДоговораКонтрагента();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииКонтрагента()
	
	Объект.Договор = Справочники.Контрагенты.ПолучитьОсновнойДоговорКонтрагента(Объект.Контрагент);
	
	ПриИзмененииДоговораКонтрагента();
	
КонецПроцедуры // ПриИзмененииКонтрагента()

&НаСервере
Процедура ПриИзмененииДоговораКонтрагента()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.ВидЦен КАК ВидЦен
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Договор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Выборка.Следующий();
		
		Если НЕ Выборка.ВидЦен.Пустая() Тогда
			Объект.ВидЦен = Выборка.ВидЦен;
		Иначе
			Объект.ВидЦен = Справочники.ВидыЦен.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииДоговораКонтрагента()

&НаКлиенте
Процедура УслугиНоменклатураПриИзменении(Элемент)
	
	//1. получить данные текущей строки
	ДанныеСтроки = Элементы.Услуги.ТекущиеДанные;
	
	//2. получить ставку НДС и единицу измерения текущей номенклатуры
	ИменаРеквизитов = "ЕдиницаИзмерения,СтавкаНДС";
	РеквизитыНоменклатуры = ОбщегоНазначенияВызовСервера.ПолучитьРеквизитыНоменклатуры(ДанныеСтроки.Номенклатура, ИменаРеквизитов);   
	
	//3. заполнить данные в строке
	Если РеквизитыНоменклатуры <> Неопределено Тогда
		ДанныеСтроки.ЕдиницаИзмерения = РеквизитыНоменклатуры.ЕдиницаИзмерения;
		ДанныеСтроки.СтавкаНДС = РеквизитыНоменклатуры.СтавкаНДС;
	Иначе 
		ДанныеСтроки.ЕдиницаИзмерения = Неопределено;
		ДанныеСтроки.СтавкаНДС = Неопределено;
	КонецЕсли;   
	
	//4. пересчитать суммы в табличной части
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммыВСтрокеТабличнойЧасти(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	//1. получить данные текущей строки
	ДанныеСтроки = Элементы.Услуги.ТекущиеДанные;
	
	//2. пересчитать суммы
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммыВСтрокеТабличнойЧасти(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	//1. получить данные текущей строки
	ДанныеСтроки = Элементы.Услуги.ТекущиеДанные;
	
	//2. пересчитать суммы
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммыВСтрокеТабличнойЧасти(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСтавкаНДСПриИзменении(Элемент)
	
	//1. получить данные текущей строки
	ДанныеСтроки = Элементы.Услуги.ТекущиеДанные;
	
	//2. пересчитать суммы
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммыВСтрокеТабличнойЧасти(ДанныеСтроки);
	
КонецПроцедуры


