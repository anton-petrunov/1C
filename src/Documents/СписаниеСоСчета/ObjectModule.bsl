
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр ДенежныеСредства Расход
	Движения.ДенежныеСредства.Записывать = Истина;
	Движение = Движения.ДенежныеСредства.ДобавитьРасход();
	Движение.Период			= Дата;
	Движение.Организация	= Организация;
	Движение.БанковскийСчет	= БанковскийСчет;
	Движение.Сумма			= СуммаДокумента;
	
	//контроль остатков (новая методика)
	Движения.ДенежныеСредства.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДенежныеСредстваОстатки.Организация КАК Организация,
		|	ДенежныеСредстваОстатки.БанковскийСчет КАК БанковскийСчет,
		|	ДенежныеСредстваОстатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	РегистрНакопления.ДенежныеСредства.Остатки(
		|			&Период,
		|			Организация = &Организация
		|				И БанковскийСчет = &БанковскийСчет) КАК ДенежныеСредстваОстатки
		|ГДЕ
		|	ДенежныеСредстваОстатки.СуммаОстаток < 0";
	
	Если Режим = РежимПроведенияДокумента.Оперативный Тогда
		Период = '00010101';	
	Иначе
		Период = Новый Граница(МоментВремени(), ВидГраницы.Включая);
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Отказ = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ТекОстаток = (Выборка.СуммаОстаток + СуммаДокумента);
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Недостаточно денежных средств для проведения операции!
						  |Текущий остаток: " + ТекОстаток + " руб.
						  |Не хватает для проведения операции: " + (-Выборка.СуммаОстаток) + " руб.";
		Сообщение.Сообщить();

	КонецЕсли; 
		
КонецПроцедуры
