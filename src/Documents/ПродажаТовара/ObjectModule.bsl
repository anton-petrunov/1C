
Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПроверитьОстатки(Отказ, Режим);
	
	СписатьПартии(); 
	
	ОтразитьПродажи();
	
КонецПроцедуры

Процедура ПроверитьОстатки(Отказ, Режим)

	// Получить данные документа ПродажаТовара
	
	Запрос = Новый Запрос; 
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаТовараТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПродажаТовараТовары.Количество) КАК Количество,
		|	СУММА(ПродажаТовараТовары.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТ_ТоварыДокумента
		|ИЗ
		|	Документ.ПродажаТовара.Товары КАК ПродажаТовараТовары
		|ГДЕ
		|	ПродажаТовараТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродажаТовараТовары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТоварыДокумента.Номенклатура КАК Номенклатура,
		|	ВТ_ТоварыДокумента.Количество КАК Количество,
		|	ВТ_ТоварыДокумента.Сумма КАК Сумма
		|ИЗ
		|	ВТ_ТоварыДокумента КАК ВТ_ТоварыДокумента";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	// Записать движения по регистру ТоварыВНаличии
	Движения.ТоварыВНаличии.Записывать = Истина;
	Пока Выборка.Следующий() Цикл
		Движение = движения.ТоварыВНаличии.ДобавитьРасход();   
		Движение.Период = Дата;                
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество; 
	КонецЦикла;            
	
	Движения.ТоварыВНаличии.Записать();
	
	// Получить остатки и отменить транзакцию, если они отрицательные
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(ТоварыВНаличииОстатки.Номенклатура) КАК НоменклатураПредставление,
		|	-ТоварыВНаличииОстатки.КоличествоОстаток КАК Дефицит
		|ИЗ
		|	РегистрНакопления.ТоварыВНаличии.Остатки(
		|			&Период,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					ВТ_ТоварыДокумента.Номенклатура КАК Номенклатура
		|				ИЗ
		|					ВТ_ТоварыДокумента КАК ВТ_ТоварыДокумента)) КАК ТоварыВНаличииОстатки
		|ГДЕ
		|	ТоварыВНаличииОстатки.КоличествоОстаток < 0";
	
	Если Режим = РежимПроведенияДокумента.Оперативный Тогда         
		Период = '00010101';
	Иначе 
		Период = Новый Граница(МоментВремени(), ВидГраницы.Включая);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Период", Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Отказ = Истина;
		Выборка = РезультатЗапроса.Выбрать(); 
		
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("На дату документа не хватает %1 ед. товара ""%2""!", Выборка.Дефицит, Выборка.НоменклатураПредставление);
			Сообщение.Сообщить();
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура СписатьПартии()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаТовараТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ПродажаТовараТовары.Количество) КАК Количество,
		|	ПродажаТовараТовары.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ.ПродажаТовара.Товары КАК ПродажаТовараТовары
		|ГДЕ
		|	ПродажаТовараТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродажаТовараТовары.Номенклатура,
		|	ПродажаТовараТовары.Сумма
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииОстатки.Номенклатура КАК Номенклатура,
		|	ПартииОстатки.Партия КАК Партия,
		|	ПартииОстатки.СрокГодности КАК СрокГодности,
		|	ПартииОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ ВТ_ОстаткиПоПартиям
		|ИЗ
		|	РегистрНакопления.Партии.Остатки(
		|			&МоментВремени,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					ВТ_Товары.Номенклатура КАК Номенклатура
		|				ИЗ
		|					ВТ_Товары КАК ВТ_Товары)) КАК ПартииОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ВТ_Товары.Количество КАК КоличествоВДокументе,
		|	ЕСТЬNULL(ВТ_ОстаткиПоПартиям.Партия, ЗНАЧЕНИЕ(Документ.ПоступлениеТовара.ПустаяСсылка)) КАК Партия,
		|	ЕСТЬNULL(ВТ_ОстаткиПоПартиям.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ВТ_ОстаткиПоПартиям.СрокГодности, НЕОПРЕДЕЛЕНО) КАК СрокГодностиОстаток
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиПоПартиям КАК ВТ_ОстаткиПоПартиям
		|		ПО ВТ_Товары.Номенклатура = ВТ_ОстаткиПоПартиям.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	СрокГодностиОстаток
		|ИТОГИ
		|	МАКСИМУМ(КоличествоВДокументе),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	// Установка явной управляемой блокировки
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Партии");
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	
	Блокировка.Заблокировать();
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Движения.Партии.Записывать = Истина;
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		ОсталосьСписать = ВыборкаНоменклатура.КоличествоВДокументе;
	
		ВыборкаПартия = ВыборкаНоменклатура.Выбрать();
	
		Пока ВыборкаПартия.Следующий() И ОсталосьСписать <> 0 Цикл
			
			Если ОсталосьСписать < ВыборкаПартия.КоличествоОстаток Тогда
				КоличествоСписания = ОсталосьСписать;
			Иначе
				КоличествоСписания = ВыборкаПартия.КоличествоОстаток;
			КонецЕсли;
			
			НоваяЗапись = Движения.Партии.ДобавитьРасход();
			НоваяЗапись.Период = Дата;
			НоваяЗапись.Партия = ВыборкаПартия.Партия;
			НоваяЗапись.Номенклатура = ВыборкаПартия.Номенклатура;
			НоваяЗапись.Количество = КоличествоСписания;
			НоваяЗапись.СрокГодности = ВыборкаПартия.СрокГодностиОстаток;
			
			ОсталосьСписать = ОсталосьСписать - КоличествоСписания;
			
		КонецЦикла;
	КонецЦикла;                                                    
	
	Движения.Партии.Записать();

КонецПроцедуры

Процедура ОтразитьПродажи()

	// Записать в регистр "Продажи"
	Движения.Продажи.Записывать = Истина;
	Движение = Движения.Продажи.Добавить();
	Для каждого Товар Из  Товары Цикл
		Движение.Номенклатура = Товар.Номенклатура;
		Движение.Количество = Товар.Количество;
		Движение.Сумма = Товар.Сумма; 
		Движение.Период = Дата;
	КонецЦикла;

	Движения.Продажи.Записать();
	
КонецПроцедуры

