
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	//1. Создать COM-объект
	Word = Новый COMОбъект("Word.Application");
	
	АдресДанныхШаблона = ПолучитьШаблонДоговораНаСервере();
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресДанныхШаблона);
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("doc");
	
	Попытка
		ДвоичныеДанные.Записать(ИмяВременногоФайла);
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось получить шаблон договора. Обратитесь в службу поддержки!";
		Сообщение.Сообщить();
		Возврат;
	КонецПопытки;
	
	//2. Создать новый документ на основе шаблона
	Документ = Word.Documents.Add(ИмяВременногоФайла);
	
	//3. Получить данные документа "Заказ клиента" для заполнения
	ПараметрыЗаполнения = ПолучитьДанныеЗаказа(ПараметрКоманды);  
	
	//4. Заполнить документ данными
	УстановитьЗначениеПараметра(Документ, "&(НомерДоговора)&", УдалитьЛидирующиеНулиУНомера(ПараметрыЗаполнения.Номер));
	УстановитьЗначениеПараметра(Документ, "&(ДатаДоговора)&", Формат(ПараметрыЗаполнения.Дата, "ДФ=dd.MM.yyyy"));
	УстановитьЗначениеПараметра(Документ, "&(НазваниеОрганизации)&", Строка(ПараметрыЗаполнения.Организация));
	УстановитьЗначениеПараметра(Документ, "&(РуководительОрганизации)&", Строка(ПараметрыЗаполнения.РуководительОрганизации));
	УстановитьЗначениеПараметра(Документ, "&(НазваниеКонтрагента)&", Строка(ПараметрыЗаполнения.Контрагент));
	УстановитьЗначениеПараметра(Документ, "&(УНП(ИНН)Организации)&", ПараметрыЗаполнения.ОрганизацияИНН);
	УстановитьЗначениеПараметра(Документ, "&(УНП(ИНН)Контрагента)&", ПараметрыЗаполнения.КонтрагентИНН);   
	УстановитьЗначениеПараметра(Документ, "&(НазваниеОрганизации)&", Строка(ПараметрыЗаполнения.Организация));
	УстановитьЗначениеПараметра(Документ, "&(РуководительОрганизации)&", Строка(ПараметрыЗаполнения.РуководительОрганизации));
	УстановитьЗначениеПараметра(Документ, "&(НазваниеКонтрагента)&", Строка(ПараметрыЗаполнения.Контрагент));

	//5. Вывести документ
	Word.Visible = Истина;
	
КонецПроцедуры     

&НаСервере
Функция ПолучитьДанныеЗаказа(СсылкаНаДокумент)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиента.Номер КАК Номер,
		|	ЗаказКлиента.Дата КАК Дата,
		|	ЗаказКлиента.Организация КАК Организация,
		|	ЗаказКлиента.Организация.Руководитель КАК РуководительОрганизации,
		|	ЗаказКлиента.Организация.ИНН КАК ОрганизацияИНН,
		|	ЗаказКлиента.Контрагент КАК Контрагент,
		|	ЗаказКлиента.Контрагент.ИНН КАК КонтрагентИНН
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Выборка.Следующий();

	ДанныеДокумента = Новый Структура;
	
	Для каждого КолонкаРезультата Из РезультатЗапроса.Колонки Цикл
		ДанныеДокумента.Вставить(КолонкаРезультата.Имя, "");
	КонецЦикла; 
	
	ЗаполнитьЗначенияСвойств(ДанныеДокумента, Выборка); 
	
	Возврат ДанныеДокумента;
	
КонецФункции // ПолучитьДанныеЗаказа()       

&НаКлиенте
Функция УстановитьЗначениеПараметра(Документ, ИмяПараметра, ЗначениеПараметра)

	Range = Документ.Content;
	Если Range.Find.Execute(ИмяПараметра) Тогда
		Range.Text = ЗначениеПараметра;
	КонецЕсли;
	
	Возврат Range;

КонецФункции // УстановитьЗначениеПараметра()  

&НаСервере
Функция ПолучитьШаблонДоговораНаСервере()

	ДвоичныеДанные = Документы.ЗаказКлиента.ПолучитьМакет("ПФ_DOC_ШаблонДоговораКуплиПродажи");
	
	АдресДанных = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
	Возврат АдресДанных;

КонецФункции // ПолучитьШаблонДоговораНаСервере()

&НаСервере
Функция УдалитьЛидирующиеНулиУНомера(Номер)
	
	Пока Лев(Номер, 1) = "0" Цикл
		Номер = Прав(Номер, СтрДлина(Номер) - 1);
	КонецЦикла;
	
	Возврат Номер;
	
КонецФункции // УдалитьЛидирующиеНулиУНомера()

