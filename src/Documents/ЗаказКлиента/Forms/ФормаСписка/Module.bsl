
&НаКлиенте
Процедура ИзменитьСтатус(Команда)
	
	//1. Получение выделенных в списке заказов
	МассивЗаказов = Элементы.Список.ВыделенныеСтроки;
	
	//2. запрос у пользователя нового статуса
	ЧтоДелатьПослеВыбораСтатуса = Новый ОписаниеОповещения("ПослеВыбораСтатуса", ЭтотОбъект, МассивЗаказов);
	ПоказатьВводЗначения(ЧтоДелатьПослеВыбораСтатуса, ,, Тип("СправочникСсылка.СтатусыЗаказовКлиентов"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораСтатуса(Значение, ДополнительныеПараметры) Экспорт

	Если Значение = Неопределено Тогда
		Возврат;	
	КонецЕсли; 
	
	//3. Передача нового статуса и списка заказов на сервер для изменения
	ИзменитьСтатусНаСервере(Значение, ДополнительныеПараметры);

	//5. оповестить списки об изменении (платформа обновит их)
	ОповеститьОбИзменении(Тип("ДокументСсылка.ЗаказКлиента"));
	
КонецПроцедуры // ПослеВыбораСтатуса()


&НаСервере
Процедура ИзменитьСтатусНаСервере(НовыйСтатус, МассивЗаказов)

	//4. изменение статусов выбранных заказов
	Для каждого Заказ Из МассивЗаказов Цикл
		ЗаказОбъект = Заказ.ПолучитьОбъект();
		ЗаказОбъект.Статус = НовыйСтатус;
		РежимЗаписи = ?(ЗаказОбъект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись);
	    ЗаказОбъект.Записать(РежимЗаписи);
	КонецЦикла; 
	
КонецПроцедуры
