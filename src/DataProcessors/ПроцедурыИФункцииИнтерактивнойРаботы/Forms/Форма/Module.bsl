
&НаКлиенте
Процедура МетодСообщить(Команда)

	Сообщить("Текущая дата: " + ТекущаяДата());
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВсеСообщения(Команда)
	
	ОчиститьСообщения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектСообщениеПользователю(Команда)
	
	//Сообщение = Новый СообщениеПользователю;
	//Сообщение.Текст = "Текущая дата: " + ТекущаяДата();
	//Сообщение.Сообщить();
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Не заполнена номенклатура";
	Сообщение.Поле = "Реквизит_Номенклатура";
	Сообщение.Сообщить();
	                                       
	
КонецПроцедуры

&НаКлиенте
Процедура МетодПредупреждение(Команда)
	
	Предупреждение("Не заполнена номенклатура",5,"Ошибка");
	Сообщить("Выведено предупреждение");
	
КонецПроцедуры

&НаКлиенте
Процедура МетодПоказатьОповещение(Команда)
	
	ПоказатьОповещениеПользователя("Текущая дата: " + ТекущаяДата(),, "пояснение", БиблиотекаКартинок.ВнешнийИсточникДанныхКуб);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСпискаНоменклатуры(Команда)
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНовойНоменклатуры(Команда)
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта");
	
	//ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЭлементаДругая");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНоменклатуры(Команда)
	
	СтруктураПараметры = Новый Структура("Ключ", Реквизит_Номенклатура);
	ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", СтруктураПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСПередачейЗначенийЗаполнения(Команда)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Наименование", "Новый товар");
	
	СтруктураПараметры = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", СтруктураПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСпискаСОтбором(Команда)
	
	//формирование структуры отбора (Ключ - имя реквизита, Значение - знаение отбора)
	СтруктураОтбора = Новый Структура("ВидКонтрагента", ПредопределенноеЗначение("Перечисление.ВидыКонтрагентов.ФизическоеЛицо"));
	
	//формирование структуры параметров формы (Ключ - имя параметра, Значение - значечние параметра)
	СтруктураПараметров = Новый Структура("Отбор", СтруктураОтбора);
	
	//открытие формы с передачей параметров
	ОткрытьФорму("Справочник.Контрагенты.ФормаСписка", СтруктураПараметров,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВводЧисла(Команда)
	
	ВведенноеЧисло = 0;
	Результат = ВвестиЧисло(ВведенноеЧисло, "Введите количество товара");
	Если Результат = Истина Тогда
		Количество = ВведенноеЧисло;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Вы ввели число " + ВведенноеЧисло;
		Сообщение.Сообщить();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВводСтроки(Команда)
	
	ВведеннаяСтрока = "";
	Результат = ВвестиСтроку(ВведеннаяСтрока, "Введите комментарий");
	Если Результат = Истина Тогда
		Комментарий = ВведеннаяСтрока;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Вы ввели число " + ВведеннаяСтрока;
		Сообщение.Сообщить();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВводДаты(Команда)
	
	ВведеннаяДата = '00010101';
	Результат = ВвестиДату(ВведеннаяДата, "Выберите дату доставки", ЧастиДаты.Дата);
	Если Результат = Истина Тогда
		ДатаДоставки = ВведеннаяДата;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Вы ввели дату: " + ВведеннаяДата;
		Сообщение.Сообщить();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВводПроизвольногоЗначения(Команда)
	
	ВведнныйКонтрагент = Неопределено;
	Результат = ВвестиЗначение(ВведнныйКонтрагент, "Введите контрагента", Тип("СправочникСсылка.Контрагенты"));
	Если Результат = Истина Тогда
		Контрагент = ВведнныйКонтрагент;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Вы выбрали контрагента: " + ВведнныйКонтрагент;
		Сообщение.Сообщить();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВводЧислаАсинхронно(Команда)
	
	ВведенноеЧисло = 0;
	ЧтоДелатьПослеВводаЧисла = Новый ОписаниеОповещения("ПослеВводаЧисла", ЭтотОбъект);
	
	ПоказатьВводЧисла(ЧтоДелатьПослеВводаЧисла, ВведенноеЧисло, "Введите количество товара"); 
	
	Сообщить("код выполняется дальше");
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаЧисла(Число, ДополнительныеПараметры) Экспорт

	Если Число = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Количество = Число;

КонецПроцедуры // ПослеВводаЧисла()


&НаКлиенте
Процедура ВводСтрокиАсинхронно(Команда)
	
	ЧтоДелатьПослеВводаСтроки = Новый ОписаниеОповещения("ПослеВводаСтроки", ЭтотОбъект);
	ПоказатьВводСтроки(ЧтоДелатьПослеВводаСтроки,,"Введите комментарий");
	
КонецПроцедуры

//
&НаКлиенте
Процедура ПослеВводаСтроки(Строка, ДополнительныеПараметры) Экспорт

	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Комментарий = Строка;

КонецПроцедуры // ПослеВводаСтроки()

&НаКлиенте
Процедура ВводДатыАсинхронно(Команда)
	
	ЧтоДелатьПослеВводаДаты = Новый ОписаниеОповещения("ПослеВводаДаты", ЭтотОбъект);
	ПоказатьВводДаты(ЧтоДелатьПослеВводаДаты, , "Введите дату доставки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаДаты(Дата, ДополнительныеПараметры) Экспорт

	Если Дата = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ДатаДоставки = Дата;

КонецПроцедуры // ()

&НаКлиенте
Процедура ВводПроизвольногоЗначенияАсинхронно(Команда)
	
	ЧтоДелатьПослеВводаЗначения = Новый ОписаниеОповещения("ПослеВводаЗначения", ЭтотОбъект);
	ПоказатьВводЗначения(ЧтоДелатьПослеВводаЗначения,,"Выберите контрагента", Тип("СправочникСсылка.Контрагенты"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаЗначения(Значение, ДополнительныеПараметры) Экспорт

	Если Значение = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Контрагент = Значение;

КонецПроцедуры // ПослеВводаЗначения()

&НаКлиенте
Процедура ВводЧиселСПередачейПараметров(Команда)
	
	ЧтоДелатьПослеВводаКоличества = Новый ОписаниеОповещения("ПослеВводаКоличества", ЭтотОбъект);
	
	ПоказатьВводЧисла(ЧтоДелатьПослеВводаКоличества, , "Введите количество товара"); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаКоличества(Число, ДополнительныеПараметры) Экспорт

	Если Число = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	//Количество = Число;
	
	ЧтоДелатьПослеВводаЦены = Новый ОписаниеОповещения("ПослеВводаЦены", ЭтотОбъект, Число);
	ПоказатьВводЧисла(ЧтоДелатьПослеВводаЦены, , "Введите цену");

КонецПроцедуры // ПослеВводаКоличество()

&НаКлиенте
Процедура ПослеВводаЦены(Число, ДополнительныеПараметры) Экспорт

	Если Число = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Количество = ДополнительныеПараметры;
	Цена = Число;

	Сумма = Количество * Цена;
	
КонецПроцедуры // ПослеВводаЦены()

&НаКлиенте
Процедура ПоказатьВопросПользователю(Команда)
	
	ЧтоДелатьПослеОтвета = Новый ОписаниеОповещения("ПослеОтветаНаВопрос", ЭтотОбъект);
	ПоказатьВопрос(ЧтоДелатьПослеОтвета, "Нужна ли доставка?", РежимДиалогаВопрос.ДаНет, 10,КодВозвратаДиалога.Да, ,КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Добавляем доставку";
		Сообщение.Сообщить();
	КонецЕсли; 

КонецПроцедуры // ПослеОтветаНаВопрос()




