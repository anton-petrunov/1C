
&НаСервере
Процедура СоздатьРеквизитыНаСервере()
	
	ТипСтрока = Новый ОписаниеТипов("Строка");
	ТипНоменклатура = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ТипЧисло = Новый ОписаниеТипов("Число");
	
	//1. Создать объект "РеквизитФормы"
	РеквизитТекст = Новый РеквизитФормы("ТекстСообщения", ТипСтрока);
	РеквизитНоменклатура = Новый РеквизитФормы("Номенклатура", ТипНоменклатура);
	РеквизитКоличество = Новый РеквизитФормы("Количество", ТипЧисло);
	РеквизитЦена = Новый РеквизитФормы("Цена", ТипЧисло);
	
	//2. Создать массив добавляемых реквизитов
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(РеквизитТекст);
	ДобавляемыеРеквизиты.Добавить(РеквизитНоменклатура);
	ДобавляемыеРеквизиты.Добавить(РеквизитКоличество);
	ДобавляемыеРеквизиты.Добавить(РеквизитЦена);
	
	//3. Вызвать метод объекта "ФормаКлиентскогоПриложения" - ИзменитьРеквизиты()
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРеквизиты(Команда)
	СоздатьРеквизитыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВсеЭлементы(Команда)
	СоздатьВсеЭлементыНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьВсеЭлементыНаСервере()
	
	Если Элементы.Найти("ТекстСообщения") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("ТекстСообщения", Тип("ПолеФормы"), Элементы.ГруппаПраво);
		НовыйЭлемент.ПутьКДанным = "ТекстСообщения";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли; 
	
	Если Элементы.Найти("Номенклатура") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("Номенклатура", Тип("ПолеФормы"), Элементы.ГруппаПраво);
		НовыйЭлемент.ПутьКДанным = "Номенклатура";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли; 
	
	Если Элементы.Найти("Количество") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("Количество", Тип("ПолеФормы"), Элементы.ГруппаПраво);
		НовыйЭлемент.ПутьКДанным = "Количество";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли; 
	
	Если Элементы.Найти("Цена") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("Цена", Тип("ПолеФормы"), Элементы.ГруппаПраво);
		НовыйЭлемент.ПутьКДанным = "Цена";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли; 

КонецПроцедуры // СоздатьВсеЭлементыНаСервере()

&НаКлиенте
Процедура СоздатьПолеВвода(Команда)
	СоздатьПолеВводаНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьПолеВводаНаСервере()
	
	НовыйЭлемент = Элементы.Добавить("Реквизит_Контрагент", Тип("ПолеФормы"), Элементы.ГруппаПраво);
	НовыйЭлемент.ПутьКДанным = "Реквизит_Контрагент";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПолеНадписи(Команда)
	СоздатьПолеНадписиНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьПолеНадписиНаСервере()
	
	Реквизит_Текст = "Это информационное сообщение";
	НовыйЭлемент = Элементы.Добавить("Реквизит_Текст", Тип("ПолеФормы"), Элементы.ГруппаПраво);
	НовыйЭлемент.ПутьКДанным = "Реквизит_Текст";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
	НовыйЭлемент.ЦветТекста = Новый Цвет(0,0,255);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПолеФлажка(Команда)
	СоздатьПолеФлажкаНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьПолеФлажкаНаСервере()
	
	НовыйЭлемент = Элементы.Добавить("Реквизит_Согласован", Тип("ПолеФормы"), Элементы.ГруппаПраво);
	НовыйЭлемент.ПутьКДанным = "Реквизит_Согласован";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
	НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	НовыйЭлемент.ВидФлажка = ВидФлажка.Выключатель; 
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПолеКалендаря(Команда)
	СоздатьПолеКалендаряНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьПолеКалендаряНаСервере()
	
	Реквизит_ДатаДоставки = ТекущаяДата();
	
	НовыйЭлемент = Элементы.Добавить("Реквизит_ДатаДоставки", Тип("ПолеФормы"), Элементы.ГруппаПраво);
	НовыйЭлемент.ПутьКДанным = "Реквизит_ДатаДоставки";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеКалендаря;
	НовыйЭлемент.ОтображатьПанельМесяцев = Истина;
	НовыйЭлемент.НачалоПериодаОтображения = НачалоМесяца(ТекущаяДата());
	НовыйЭлемент.КонецПериодаОтображения = КонецМесяца(ТекущаяДата());
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПолеПереключателя(Команда)
	СоздатьПолеПереключателяНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьПолеПереключателяНаСервере()
	
	НовыйЭлемент = Элементы.Добавить("Реквизит_ВидыДнейКалендаря", Тип("ПолеФормы"), Элементы.ГруппаПраво);
	НовыйЭлемент.ПутьКДанным = "Реквизит_ВидыДнейКалендаря";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеПереключателя;
	НовыйЭлемент.СписокВыбора.Добавить(Перечисления.ВидыДнейКалендаря.Рабочий);
	НовыйЭлемент.СписокВыбора.Добавить(Перечисления.ВидыДнейКалендаря.Предпраздничный);
	НовыйЭлемент.СписокВыбора.Добавить(Перечисления.ВидыДнейКалендаря.Праздник);
	НовыйЭлемент.СписокВыбора.Добавить(Перечисления.ВидыДнейКалендаря.Суббота);
	НовыйЭлемент.СписокВыбора.Добавить(Перечисления.ВидыДнейКалендаря.Воскресенье);
	
	НовыйЭлемент.ВидПереключателя = ВидПереключателя.Тумблер;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьТаблицуФормы(Команда)
	СоздатьТаблицуФормыНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьТаблицуФормыНаСервере()
	
	//сначала создаем таблицу формы
	ЭлементТаблица = Элементы.Добавить("ТаблицаТоваров", Тип("ТаблицаФормы"), Элементы.ГруппаПраво);
	ЭлементТаблица.ПутьКДанным = "ТаблицаТоваров";
	ЭлементТаблица.УстановитьДействие("ПриОкончанииРедактирования", "РассчитатьИтогВТаблице");
	ЭлементТаблица.Подвал = Истина;
	
	//колонка "Номенклатура"
	КолонкаНоменклатура = Элементы.Добавить("Номенклатура", Тип("ПолеФормы"), ЭлементТаблица);
	КолонкаНоменклатура.ПутьКДанным = "ТаблицаТоваров.Номенклатура";
	КолонкаНоменклатура.Вид = ВидПоляФормы.ПолеВвода;
	
	//колонка "Количество"
	КолонкаКоличество = Элементы.Добавить("Количество", Тип("ПолеФормы"), ЭлементТаблица);
	КолонкаКоличество.ПутьКДанным = "ТаблицаТоваров.Количество";
	КолонкаКоличество.Вид = ВидПоляФормы.ПолеВвода;
	КолонкаКоличество.УстановитьДействие("ПриИзменении", "РассчитатьСумму");
	
	//колонка "Цена"
	КолонкаЦена = Элементы.Добавить("Цена", Тип("ПолеФормы"), ЭлементТаблица);
	КолонкаЦена.ПутьКДанным = "ТаблицаТоваров.Цена";
	КолонкаЦена.Вид = ВидПоляФормы.ПолеВвода;
	КолонкаЦена.УстановитьДействие("ПриИзменении", "РассчитатьСумму");
	КолонкаЦена.ТекстПодвала = "Итого сумма:";

	//колонка "Сумма"
	КолонкаСумма = Элементы.Добавить("Сумма", Тип("ПолеФормы"), ЭлементТаблица);
	КолонкаСумма.ПутьКДанным = "ТаблицаТоваров.Сумма";
	КолонкаСумма.Вид = ВидПоляФормы.ПолеВвода;
	КолонкаСумма.ПутьКДаннымПодвала = "ИтогоСумма";
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСумму()

	//1. Получить данные строки, с которой работает пользователь
	ТекСтрока = Элементы.ТаблицаТоваров.ТекущиеДанные;
	
	//2. Пересчет суммы
	ТекСтрока.Сумма = ТекСтрока.Количество * ТекСтрока.Цена;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекстСообщения(Команда)
	
	ЭтотОбъект["ТекстСообщения"] = "Привет, мир!";
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьИтогВТаблице()

	ИтогоСумма = ЭтотОбъект["ТаблицаТоваров"].Итог("Сумма");

КонецПроцедуры // РассчитатьИтогВТаблице()

&НаКлиенте
Процедура СоздатьКнопку(Команда)
	СоздатьКнопкуНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьКнопкуНаСервере()
	
	//1. создание команды
	НоваяКоманда = Команды.Добавить("ПоказатьТекущуюДату");
	НоваяКоманда.Действие = "СообщитьТекущуюДату";
	
	//2. создание элемента формы (кнопки)
	НоваяКнопка = Элементы.Добавить("ПоказатьТекущуюДату", Тип("КнопкаФормы"), Элементы.ГруппаПраво);
	НоваяКнопка.ИмяКоманды = "ПоказатьТекущуюДату";
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьТекущуюДату()

	ДатаФормат = Формат(ТекущаяДата(), "ДЛФ=DD");
	Сообщить(ДатаФормат);

КонецПроцедуры // СообщитьТекущуюДату()





