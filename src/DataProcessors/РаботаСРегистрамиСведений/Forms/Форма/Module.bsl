
&НаСервере
Процедура СозданиеЗаписейНаСервере()
	
	//1. создание одной записи
	ВалютаUSD = Справочники.Валюты.НайтиПоКоду("840");
	Если НЕ ВалютаUSD.Пустая() Тогда
		
		НоваяЗапись = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
		НоваяЗапись.Период = ТекущаяДата();
		НоваяЗапись.Валюта = ВалютаUSD;
		НоваяЗапись.Курс = 75.76;
		НоваяЗапись.Кратность = 1;
		
		НоваяЗапись.Записать();
		
	Иначе
		Сообщить("НЕ найдена валюта");
	КонецЕсли; 
	
	//2. Создание набора записей
	Если НЕ ВалютаUSD.Пустая() Тогда

		НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Валюта.Установить(ВалютаUSD);
		НаборЗаписей.Отбор.Период.Установить(НачалоДня(ТекущаяДата()) - 1);
		
		//добавление записи в набор
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Период = НачалоДня(ТекущаяДата()) - 1;
		НоваяЗапись.Валюта = ВалютаUSD;
		НоваяЗапись.Курс = 80;
		НоваяЗапись.Кратность = 1;
		
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
	//3. Заполнение регистра сведений
	ТекДата = НачалоМесяца(ТекущаяДата());
	КонДата = КонецМесяца(ТекущаяДата());
	Пока ТекДата <= КонДата Цикл
		
		НоваяЗапись = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
		НоваяЗапись.Период = ТекДата;
		НоваяЗапись.Валюта = ВалютаUSD;
		НоваяЗапись.Курс = 70 + ДеньНедели(ТекДата);
		НоваяЗапись.Кратность = 1;
		
		НоваяЗапись.Записать();
		
		ТекДата = ТекДата + 86400;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеЗаписей(Команда)
	СозданиеЗаписейНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПоискЗаписейНаСервере()

	ВалютаUSD = Справочники.Валюты.НайтиПоКоду("840");
	
	Если ВалютаUSD.Пустая() Тогда
		Сообщить("Не найдена валюта");
		Возврат;
	КонецЕсли; 
	
	СтруктураПоиска = Новый Структура("Валюта", ВалютаUSD);
	Результат = РегистрыСведений.КурсыВалют.Получить('20211004', СтруктураПоиска);
	Сообщить("" + Результат.Курс + " за " + Результат.Кратность + " ед.");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискЗаписей(Команда)
	ПоискЗаписейНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьПоследнее(Команда)
	ПолучитьПоследнееНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПолучитьПоследнееНаСервере()

	ВалютаUSD = Справочники.Валюты.НайтиПоКоду("840");
	
	Если ВалютаUSD.Пустая() Тогда
		Сообщить("Не найдена валюта");
		Возврат;
	КонецЕсли; 
	
	СтруктураПоиска = Новый Структура("Валюта", ВалютаUSD);
	Результат = РегистрыСведений.КурсыВалют.ПолучитьПоследнее('20211004', СтруктураПоиска);
	Сообщить("" + Результат.Курс + " за " + Результат.Кратность + " ед.");

КонецПроцедуры // ()


&НаСервере
Процедура ПолучитьПервоеНаСервере()
	
	ВалютаUSD = Справочники.Валюты.НайтиПоКоду("840");
	
	Если ВалютаUSD.Пустая() Тогда
		Сообщить("Не найдена валюта");
		Возврат;
	КонецЕсли; 
	
	СтруктураПоиска = Новый Структура("Валюта", ВалютаUSD);
	Результат = РегистрыСведений.КурсыВалют.ПолучитьПервое('20211004', СтруктураПоиска);
	Сообщить("" + Результат.Курс + " за " + Результат.Кратность + " ед.");

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьПервое(Команда)
	ПолучитьПервоеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВыборЗаписейНаСервере()
	
	ВалютаUSD = Справочники.Валюты.НайтиПоКоду("840");
	
	Если ВалютаUSD.Пустая() Тогда
		Сообщить("Не найдена валюта");
		Возврат;
	КонецЕсли; 
	
	СтруктураПоиска = Новый Структура("Валюта", ВалютаUSD);
	
	ВыборкаКурсы = РегистрыСведений.КурсыВалют.Выбрать('20211005', '20211010', СтруктураПоиска);
	Пока ВыборкаКурсы.Следующий() Цикл
		Сообщить("" + ВыборкаКурсы.Период + " - " + ВыборкаКурсы.Курс);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборЗаписей(Команда)
	ВыборЗаписейНаСервере();
КонецПроцедуры

&НаСервере
Процедура СрезПоследнихНаСервере()
	
	ВалютаUSD = Справочники.Валюты.НайтиПоКоду("840");
	
	Если ВалютаUSD.Пустая() Тогда
		Сообщить("Не найдена валюта");
		Возврат;
	КонецЕсли; 

	СтруктураОтбора = Новый Структура("Валюта", ВалютаUSD);
	КурсыUSD = РегистрыСведений.КурсыВалют.СрезПоследних('20211010', СтруктураОтбора);
	Для каждого ТекСтрока Из КурсыUSD Цикл
		Сообщить("" + ТекСтрока.Период + " - " + ТекСтрока.Курс);
	КонецЦикла; 
	

КонецПроцедуры

&НаКлиенте
Процедура СрезПоследних(Команда)
	СрезПоследнихНаСервере();
КонецПроцедуры

&НаСервере
Процедура СрезПервыхНаСервере()
	ВалютаUSD = Справочники.Валюты.НайтиПоКоду("840");
	
	Если ВалютаUSD.Пустая() Тогда
		Сообщить("Не найдена валюта");
		Возврат;
	КонецЕсли; 

	СтруктураОтбора = Новый Структура("Валюта", ВалютаUSD);
	КурсыUSD = РегистрыСведений.КурсыВалют.СрезПервых('20211010', СтруктураОтбора);
	Для каждого ТекСтрока Из КурсыUSD Цикл
		Сообщить("" + ТекСтрока.Период + " - " + ТекСтрока.Курс);
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура СрезПервых(Команда)
	СрезПервыхНаСервере();
КонецПроцедуры

&НаСервере
Процедура ИзменениеЗаписейНаСервере()
	
	ВалютаUSD = Справочники.Валюты.НайтиПоКоду("840");
	
	Если ВалютаUSD.Пустая() Тогда
		Сообщить("Не найдена валюта");
		Возврат;
	КонецЕсли; 
	
	//создаем пустой набор записей
	НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
	
	//накладываем отбор по валюте
	НаборЗаписей.Отбор.Валюта.Установить(ВалютаUSD);
	
	//читаем данные из базы в набор записей
	НаборЗаписей.Прочитать();
	
	//меняем записи в наборе записей
	Для каждого Запись Из НаборЗаписей Цикл
		Запись.Курс = Запись.Курс + 1;
	КонецЦикла; 
	
	//записываем данные в базу
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеЗаписей(Команда)
	ИзменениеЗаписейНаСервере();
КонецПроцедуры

&НаСервере
Процедура УдалениеЗаписейНаСервере()
	
	//удаление записей по доллару с 01.10 по 05.10
	ВалютаUSD = Справочники.Валюты.НайтиПоКоду("840");
	
	Если ВалютаUSD.Пустая() Тогда
		Сообщить("Не найдена валюта");
		Возврат;
	КонецЕсли; 
	
	//создаем пустой набор записей
	НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
	
	//устанавливаем отбор по валюте
	НаборЗаписей.Отбор.Валюта.Установить(ВалютаUSD);
	
	//читаем данные из базы в набор записей
	НаборЗаписей.Прочитать();
	
	МассивКУдалению = Новый Массив;
	Для каждого Запись Из НаборЗаписей Цикл
		Если Запись.Период >= '20211001' И Запись.Период <= '20211005' Тогда
			МассивКУдалению.Добавить(Запись);
		КонецЕсли; 
	КонецЦикла; 
	
	Для каждого УдаляемаяЗапись Из МассивКУдалению Цикл
		НаборЗаписей.Удалить(УдаляемаяЗапись);
	КонецЦикла; 
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалениеЗаписей(Команда)
	УдалениеЗаписейНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОчисткаРегистраНаСервере()
	
	//создаем пустой набор записей
	НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
	
	//записываем его в регистр
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчисткаРегистра(Команда)
	ОчисткаРегистраНаСервере();
КонецПроцедуры
