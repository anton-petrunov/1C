
&НаКлиенте
Процедура ВыполнитьЗапросБезПараметров(Команда)
	
	ВыполнитьЗапросБезПараметровНаСервере();
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьЗапросБезПараметровНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Ссылка,
		|	Контрагенты.ИНН КАК ИНН
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Покупатель = ИСТИНА";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "" + ВыборкаДетальныеЗаписи.Ссылка + " (ИНН: " + ВыборкаДетальныеЗаписи.ИНН + ")";
		Сообщение.Сообщить();
		
	КонецЦикла;
	
КонецФункции // ВыполнитьЗапрос()

&НаКлиенте
Процедура ВыполнитьЗапросСПараметрами(Команда)
	ВыполнитьЗапросСПараметрамиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗапросСПараметрамиНаСервере()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|ГДЕ
		|	ЗаказКлиентаТовары.Номенклатура = &Номенклатура";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Заказы, в которых встречается номенклатура:";
		Сообщение.Сообщить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = Выборка.Ссылка;
			Сообщение.Сообщить();
		КонецЦикла;
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не найдено ни одного заказа";
		Сообщение.Сообщить();		
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяТовары.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РасходнаяНакладная.Товары КАК РасходнаяНакладнаяТовары
		|ГДЕ
		|	РасходнаяНакладнаяТовары.Номенклатура = &Номенклатура
		|	И РасходнаяНакладнаяТовары.Ссылка.Склад = &Склад";
	
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Выборка.Ссылка;
		Сообщение.Сообщить();
	КонецЦикла;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапросПакетный(Команда)
	ВыполнитьЗапросПакетныйНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗапросПакетныйНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Код КАК Код,
		|	Номенклатура.Наименование КАК Наименование,
		|	Номенклатура.Артикул КАК Артикул
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Контрагенты.Код КАК Код,
		|	Контрагенты.Наименование КАК Наименование,
		|	Контрагенты.ИНН КАК ИНН
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Склады.Наименование КАК Наименование,
		|	Склады.Код КАК Код,
		|	Склады.ТипСклада КАК ТипСклада
		|ИЗ
		|	Справочник.Склады КАК Склады";
	
	//1. Получить результат последнего пакета запроса
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Выборка.Наименование;
		Сообщение.Сообщить();
	КонецЦикла;
	
	//2. Получить результаты всех запросов пакета
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	РезультатНоменклатура = МассивРезультатов[0];
	РезультатКонтрагенты = МассивРезультатов[1];
	РезультатСклады = МассивРезультатов[2];
	
	ВыборкаНоменклатура = РезультатНоменклатура.Выбрать();
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ВыборкаНоменклатура.Наименование;
		Сообщение.Сообщить();
	КонецЦикла; 
	
	ВыборкаКонтрагентов = РезультатКонтрагенты.Выбрать();
	Пока ВыборкаКонтрагентов.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ВыборкаКонтрагентов.Наименование;
		Сообщение.Сообщить();
	КонецЦикла; 
	
	ВыборкаСклады = РезультатСклады.Выбрать();
	Пока ВыборкаСклады.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ВыборкаСклады.Наименование;
		Сообщение.Сообщить();
	КонецЦикла;
	
	//3. Получить результаты с промежуточными данными
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.ВидНоменклатуры = &ВидНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(
		|			,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					ВТ_Товары.Ссылка КАК Ссылка
		|				ИЗ
		|					ВТ_Товары КАК ВТ_Товары)) КАК ТоварыНаСкладахОстатки";
	
	Запрос.УстановитьПараметр("ВидНоменклатуры", ВидНоменклатуры);
	
	МассивРезультатов = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатЗапросаЧерезВыборкуПрямойОбход(Команда)
	ОбработатьРезультатЗапросаЧерезВыборкуПрямойОбходНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатЗапросаЧерезВыборкуПрямойОбходНаСервере()
	
	ТекстРезультат = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Склад КАК Склад,
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Остаток
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки КАК ТоварыНаСкладахОстатки
		|ИТОГИ
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Склад";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаПрямойОбход = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаПрямойОбход.Следующий() Цикл
		
		Строка = "" + ВыборкаПрямойОбход.Склад + "; " + ВыборкаПрямойОбход.Номенклатура + "; " + ВыборкаПрямойОбход.Остаток;
		ТекстРезультат = ТекстРезультат + Строка + Символы.ПС;
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатЗапросаЧерезВыборкуОбходПоГруппировкам(Команда)
	ОбработатьРезультатЗапросаЧерезВыборкуОбходПоГруппировкамНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатЗапросаЧерезВыборкуОбходПоГруппировкамНаСервере()
	
	ТекстРезультат = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Склад КАК Склад,
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Остаток
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки КАК ТоварыНаСкладахОстатки
		|ИТОГИ
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Склад";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаПоСкладам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоСкладам.Следующий() Цикл
		
		//обход итогов по складам
		Строка = "" + ВыборкаПоСкладам.Склад + "; " + ВыборкаПоСкладам.Номенклатура + "; " + ВыборкаПоСкладам.Остаток;
		ТекстРезультат = ТекстРезультат + Строка + Символы.ПС;
		
		ВыборкаПоНоменклатуре = ВыборкаПоСкладам.Выбрать();
		Пока ВыборкаПоНоменклатуре.Следующий() Цикл
			//обход детальных записей
			Строка = "----" + ВыборкаПоНоменклатуре.Склад + "; " + ВыборкаПоНоменклатуре.Номенклатура + "; " + ВыборкаПоНоменклатуре.Остаток;
			ТекстРезультат = ТекстРезультат + Строка + Символы.ПС;					
		КонецЦикла; 
		
	КонецЦикла; 
	
	ВыполнитьЗапросПоДокументу();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗапросПоДокументу()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Ссылка КАК Ссылка,
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Количество КАК Количество,
		|	ЗаказКлиентаТовары.СуммаВсего КАК СуммаВсего
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|ИТОГИ ПО
		|	Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТекстРезультат = "";
	Пока ВыборкаСсылка.Следующий() Цикл
		// Вставить обработку выборки ВыборкаСсылка
	    Строка = ВыборкаСсылка.Ссылка;
		ТекстРезультат = ТекстРезультат + Строка + Символы.ПС;
		
		ВыборкаДетальныеЗаписи = ВыборкаСсылка.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Строка = "---" + ВыборкаДетальныеЗаписи.Номенклатура + "; " + ВыборкаДетальныеЗаписи.Количество + "; " + ВыборкаДетальныеЗаписи.СуммаВсего;
			ТекстРезультат = ТекстРезультат + Строка + Символы.ПС;
		КонецЦикла;
		
		ТекстРезультат = ТекстРезультат + "------------------------------------" + Символы.ПС;
		
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА

КонецПроцедуры // ВыполнитьЗапросПоДокументу()

&НаКлиенте
Процедура ОбработатьРезультатЗапросаСТабЧастьюЧерезВыборкуПрямойОбход(Команда)
	ОбработатьРезультатЗапросаСТабЧастьюЧерезВыборкуПрямойОбходНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатЗапросаСТабЧастьюЧерезВыборкуПрямойОбходНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиента.Ссылка КАК Ссылка,
		|	ЗаказКлиента.Номер КАК Номер,
		|	ЗаказКлиента.Дата КАК Дата,
		|	ЗаказКлиента.Статус КАК Статус,
		|	ЗаказКлиента.Товары.(
		|		Ссылка КАК Ссылка,
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура КАК Номенклатура,
		|		Количество КАК Количество,
		|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|		Цена КАК Цена,
		|		СтавкаНДС КАК СтавкаНДС,
		|		Сумма КАК Сумма,
		|		СуммаНДС КАК СуммаНДС,
		|		СуммаВсего КАК СуммаВсего,
		|		Резерв КАК Резерв
		|	) КАК Товары
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ТекстРезультат = "";
	Пока Выборка.Следующий() Цикл
		//обработка полей основной таблицы
		Строка = Выборка.Ссылка;
		ТекстРезультат = ТекстРезультат + Строка + Символы.ПС;
		
		//получение выборки для поля "Товары" (табличная часть)
		ВыборкаТовары = Выборка.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			//обход выборки по таб. части
			Строка = "---" + ВыборкаТовары.Номенклатура + "; " + ВыборкаТовары.Количество + "; " + ВыборкаТовары.СуммаВсего;
			ТекстРезультат = ТекстРезультат + Строка + Символы.ПС;
		КонецЦикла; 
		
		ТекстРезультат = ТекстРезультат + "------------------------------------" + Символы.ПС;
		
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатЗапросаЧерезТаблицуЗначений(Команда)
	ОбработатьРезультатЗапросаЧерезТаблицуЗначенийНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатЗапросаЧерезТаблицуЗначенийНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Склад КАК Склад,
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Остаток
		|ИЗ                 
		|	РегистрНакопления.ТоварыНаСкладах.Остатки КАК ТоварыНаСкладахОстатки
		|ИТОГИ
		|	СУММА(Остаток)
		|ПО
		|	Склад";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	//1. Выгрузка результата в таблицу значений
	ТаблицаОстатков = РезультатЗапроса.Выгрузить();
	
	Остатки.Загрузить(ТаблицаОстатков);
	
	//2. Выгрузка результата в дерево значений
	ДеревоОстатков = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецПроцедуры



